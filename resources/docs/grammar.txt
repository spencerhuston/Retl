<type> ::= 'int' | 'bool' | 'char' | 'string' | 'null'
    | <type> '->' <type>
    | '('[<type>[','<type>]*]')' '->' '('[<type>[','<type>]*]')'
    | 'list''['<type>']'
    | 'dict''['<type>','<type>']'
    | 'tuple''['<type>[','<type>]*']'
    | 'schema'
    | <ident>

<literal> ::= <int> | <bool> | <char> | <string> | <null> | <range>
<int> ::= [integer]
<bool> ::= 'true' | 'false'
<char> ::= '''[character]'''
<string> ::= '"'[character]*'"'
<null> ::= 'null'
<range> ::= <int>'..'<int>

<atom> ::= <literal>
    | '('<smp>')'
    | <ident>['.'<ident>]

<param> ::= <ident>[':' <type>]
<lambda> ::= '|'[<param>[','<param>]*]'|' '->' <type> '{'<_exp>'}'

<app> ::= <atom>['('[<smp>[','<smp>]*]')']

<pattern> ::= <ident>':'<type> ['if' <smp>] | <literal>['|'<literal>]* | <range> | '_'
<match> ::= 'match' <atom> '{' 'case' <pattern> '=>' <smp>[',''case' <pattern> '=>' <smp>]*'}'

<tight> ::= <app>['|>'<app>]
    | '{'<_exp>'}'

<arithOp> ::= '+' | '-' | '*' | '/' | '%' |
<boolOp> ::= '<' | '>' | '<=' | '>=' | '==' | 'not' | 'and' | 'or'
<op> ::= <arithOp> | <boolOp> | '++'

<utight> ::= [<op>]<tight>

<alias> ::= 'alias' <ident> '=' <type>

<collection> ::= '['[<smp>[':'<smp>][','<smp>[':'<smp>]]*]']'

<smp> ::= <utight>[<op><utight>]
    | 'if' '('<smp>')' '{' <smp> '}' ['else' '{' <smp> '}']
    | <collection>['++' <collection>]
    | '('<smp>[','<smp>]')'
    | '$''{'<ident>':'<type>[','<ident>':'<type>]*'}'
    | <match>
    | <lambda>
    | <alias>

<_exp> ::= <smp>[';'<_exp>]
    | 'let' <ident> [':' <type>] '=' <smp>';'<_exp>